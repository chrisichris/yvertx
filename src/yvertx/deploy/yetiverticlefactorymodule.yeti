/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

module yvertx.deploy.yetiverticlefactorymodule;

import java.lang:ClassLoader,
                 Throwable,
                 Thread;

import java.io.File;
import java.net:URLDecoder,
                URLClassLoader;

import org.vertx.java.core.impl:VertxInternal;
import org.vertx.java.deploy: Verticle;
import org.vertx.java.deploy.impl: VerticleFactory,
                                    VerticleManager,
                                    VertxLocator;

class MockVerticle extends Verticle
    void start() 
        println "\nFix Error and save to redeploy.";
        println "waiting for redeploy ....",
end;
    

class YetiVerticleFactory extends VerticleFactory
    var manager = () as ~VerticleManager,
    
    void init(VerticleManager mgr)
        manager := mgr,
    
    String getLanguage() "yeti",
    
    boolean isFactoryFor(String main)
        (strEnds? main ".yeti"),
    
    Verticle createVerticle(String verticleName, ClassLoader loader)
        import yeb:YetiClassLoader;
        import yeti.lang.compiler:CompileException;
        import java.lang.RuntimeException;
        import yeti.lang:Fun,
                         Struct;
       
        //load the mod.json to see wheter autoredeploy
        autoRedeploy = 
            (std = load yeb.std;
            ins = loader#getResourceAsStream("mod.json");
            if defined? ins then
                js = inputHandle ins "UTF-8"
                    |> getContents
                    |> std.parseJson;
                std.maybeDefined \false std.isBoolean js.``auto-redeploy``
            else
                false
            fi);
        //the file name
        fileName = 
            (strReplace "." "/" 
                (strLeft verticleName ((strLength verticleName) - 5)))
            ^ strRight verticleName ((strLength verticleName) - 5);
        
        resourceURL = loader#getResource(fileName);
        if not defined? resourceURL then
            failWith "no verticle found for \(fileName) given \(verticleName)";
        fi;
        sourceFile = 
            new File(URLDecoder#decode(resourceURL#getFile(), "UTF-8"));
        
        if not sourceFile#canRead() then
            failWith ("can not read file \(sourceFile#getAbsolutePath())"
                    ^"current dir is: \((new File("."))#getAbsolutePath())");
        fi;
        
        //the targetdir
        targetDir = 
            (load yeb.std;
            name = "vertx_yeti_classes_"
                ^ hashSHA1 sourceFile#getAbsolutePath();
            tmpDir = new File(System#getProperty("java.io.tmpdir"));
            newFile = new File(tmpDir, name);
            if not newFile#exists() then
                _ = newFile#mkdirs();
            fi;
            newFile#deleteOnExit();
            newFile);
        
        //compile with current loader
        ocl = Thread#currentThread()#getContextClassLoader();
        Thread#currentThread()#setContextClassLoader(loader);
        try
            eval = load yeti.lang.compiler.eval;
            println "[yetic] Compiling to: \(targetDir#getAbsolutePath())";
            eval.compileYetiFiles [To targetDir#getAbsolutePath(),
                                   Warn println] 
                                  [] [sourceFile#getAbsolutePath()];

            cl = new URLClassLoader(array [targetDir#toURI()#toURL()],
                                       loader);
            Thread#currentThread()#setContextClassLoader(cl);

            //create the url classLoader
            {className,functionName}  = 
                (pa = if strEnds? verticleName ".yeti" 
                    or strEnds? verticleName ".teti" then
                    strLeft verticleName (strLength verticleName - 5)
                 else
                    verticleName
                 fi;
                 cut = strIndexOf pa ":" 0;
                 if cut < 0 then
                    {className = pa, functionName = none}
                 else 
                    className = strLeft pa cut;
                    fn = strRight pa (cut + 1);
                    if strLength fn > 0 then
                        {className, functionName = Some fn}
                    else
                        {className, functionName = none}
                    fi
                 fi);
            moduleCl = cl#loadClass(className);
            rs = moduleCl#getMethod("eval",() as ~Class[])
                     #invoke(() as ~Object,() as ~Object[]);
            
            //get the function for creating the verticle
            fn = case functionName of
                None _ : rs unsafely_as ~Fun;
                Some funName :
                    ((rs unsafely_as ~Struct)#get(funName is string))
                        unsafely_as ~Fun;
                esac;

            //invoke the function
            fn#apply(() as ~Object) unsafely_as ~Verticle;
        catch CompileException ex:
            println "Yeti Compilation Error: \(ex#getMessage())";
            if autoRedeploy then
                (new MockVerticle()) as ~Verticle;
            else
                import java.lang:ClassNotFoundException;
                throw new ClassNotFoundException(ex#getMessage());
            fi
        finally
            Thread#currentThread()#setContextClassLoader(ocl);
        yrt,

    void reportException(Throwable t)
        manager#getLogger()#error("Exception in Yeti verticle script", t),

end;


none;
