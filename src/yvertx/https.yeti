/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

module yvertx.https;

import java.util.Map;
                 
import org.vertx.java.core: Handler, Vertx;

import org.vertx.java.core.http:HttpServer,
                                HttpServerRequest,
                                WebSocket,
                                ServerWebSocket,
                                RouteMatcher;

yvertx = load yvertx;

toRequestHandler fn is (~HttpServerRequest -> 'a) -> ~Handler =
        yvertx.toHandler fn;

requestHandler server handler =
   (server is ~HttpServer)#requestHandler(toRequestHandler handler);
        

dataHandler request handler =
   (request is ~HttpServerRequest)#dataHandler(yvertx.toBufferHandler handler);

endHandler request handler =
   (request is ~HttpServerRequest)#endHandler(yvertx.toHandler handler);

exceptionHandler request handler =
   (request is ~HttpServerRequest)#exceptionHandler(
        yvertx.toExceptionHandler handler);
   
bodyHandler request handler =
   (request is ~HttpServerRequest)#bodyHandler(
        yvertx.toBufferHandler handler);

   
create vertx = 
    (vertx is ~Vertx)#createHttpServer();

createWithHandler vertx handler = 
    (vertx is ~Vertx)#createHttpServer()#requestHandler(toRequestHandler handler);
    
close server handler =
    (server is ~HttpServer)#close(yvertx.toSimpleHandler handler);

_routeMatcher () =
   (routeMatcher = new RouteMatcher();
     toHa h = yvertx.toHandler (h is ~HttpServerRequest -> 'a);
     
    {
        all route h = routeMatcher#all(route,toHa h), 
        allWithRegex route h = routeMatcher#allWithRegEx(route,toHa h), 
        connect route h = routeMatcher#connect(route,toHa h),
        connectWithRegEx route h = routeMatcher#connectWithRegEx(route,toHa h),
        delete route h = routeMatcher#delete(route,toHa h), 
        deleteWithRegEx route h = routeMatcher#deleteWithRegEx(route,toHa h), 
        _get route h = routeMatcher#get(route,toHa h),
        getWithRegEx route h = routeMatcher#getWithRegEx(route,toHa h), 
        head route h = routeMatcher#head(route,toHa h), 
        headWithRegEx route h = routeMatcher#headWithRegEx(route,toHa h), 
        options route h = routeMatcher#options(route,toHa h), 
        optionsWithRegEx route h = routeMatcher#optionsWithRegEx(route,toHa h), 
        patch route h = routeMatcher#patch(route,toHa h), 
        patchWithRegEx route h = routeMatcher#patchWithRegEx(route,toHa h), 
        post route h = routeMatcher#post(route,toHa h), 
        postWithRegEx route h = routeMatcher#postWithRegEx(route,toHa h), 
        put route h = routeMatcher#put(route,toHa h), 
        putWithRegEx route h = routeMatcher#putWithRegEx(route,toHa h), 
        trace route h = routeMatcher#trace(route,toHa h), 
        traceWithRegEx route h = routeMatcher#traceWithRegEx(route,toHa h), 
        noMatch h = routeMatcher#noMatch(toHa h),
        routeMatcher,
        handle req = routeMatcher#handle(req),
        createServer vertx = 
            (vertx is ~Vertx)#createHttpServer()#requestHandler(routeMatcher)
    });        
        
 
putHeaders request headers =
   ((request is ~HttpServerRequest)#response#headers()#putAll(
        (headers is hash<string,string>) as ~Map);
    ());

putTrailers request trailers =
    ((request is ~HttpServerRequest)#response#trailers()#putAll(
        (trailers is hash<string,string>) as ~Map);
    ());
    
{
    toRequestHandler,
    requestHandler,
    dataHandler,
    endHandler,
    exceptionHandler,
    bodyHandler,
    createWithHandler,
    create,
    close,
    putHeaders,
    putTrailers,
    routeMatcher = _routeMatcher,
    
    
    params req =
       (ret = [:];
        forJavaMap (req is ~HttpServerRequest)#params() do key value:
            ret.[string key] := if defined? value then string value else "" fi;
        done;
        ret),
        
    headers req =
        (ret = [:];
        forJavaMap (req is ~HttpServerRequest)#headers() do key value:
            ret.[string key] := if defined? value then string value else "" fi;
        done;
        ret),
    
}
 
