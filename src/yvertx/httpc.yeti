/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

module yvertx.httpc;

                 
import java.util.Map;                 

import org.vertx.java.core: Handler, Vertx;
import org.vertx.java.core.http:HttpClientResponse,
                                HttpClientRequest,
                                WebSocket,
                                HttpClient;

yvertx = load yvertx;

toResponseHandler fn is (~HttpClientResponse -> ()) -> ~Handler =
        yvertx.toHandler fn;

responseHandler response fn = 
        fn (response is ~HttpClientResponse);

dataHandler response handler =
   (response is ~HttpClientResponse)#dataHandler(
        yvertx.toBufferHandler handler);

endHandler response handler =   
    (response is ~HttpClientResponse)#endHandler(
            yvertx.toHandler handler);

bodyHandler response handler =
   (response is ~HttpClientResponse)#bodyHandler(
        yvertx.toBufferHandler handler);

exceptionHandler response handler =
   (response is ~HttpClientResponse)#exceptionHandler(
        yvertx.toExceptionHandler handler);

   
   
create vertx hostPort = 
   ({port,host} = yvertx.helper.hostAndPort hostPort; 
    (vertx is ~Vertx)#createHttpClient()#setPort(port)#setHost(host));


    
putHeaders request headers =
   ((request is ~HttpClientRequest)#headers()#putAll(
        (headers is hash<string,string>) as ~Map);
    request);
    
request client muri headers handler =
    (req = case muri of
    Connect uri: 
        (client is ~HttpClient)#connect(uri is string,
                                    toResponseHandler handler);
    Delete uri:    
        client#delete(uri is string, toResponseHandler handler);
    Get uri: 
        client#get(uri is string, toResponseHandler handler);
    Head uri:
        client#head(uri is string, toResponseHandler handler);
    Options uri:
        client#options(uri is string, toResponseHandler handler);
    Patch uri:
        client#patch(uri is string, toResponseHandler handler);
    Put uri:
        client#put(uri is string, toResponseHandler handler);
    Post uri:
        client#post(uri is string, toResponseHandler handler);
    Trace uri:
        client#trace(uri is string, toResponseHandler handler);
    Request {method, uri}:
        client#request(method is string, uri is string,
            toResponseHandler handler); 
    esac;
    putHeaders req headers);

generalRequest client method uri headers handler =
    request client (Request {method, uri}) headers handler;
    
getNow client uri headers handler =
   (client is ~HttpClient)#getNow(uri is string, 
                                   headers is hash<string,string>,
                                   toResponseHandler handler);
                                   
getBodyNow client uri headers handler =
   getNow client uri headers 
                do resp:
                    bodyHandler resp do buffer:
                        handler {buffer, resp}
                    done
                done;

connectWebsocket client uri handler =
    (client is ~HttpClient)#connectWebsocket(uri is string,
                            yvertx.toHandler (handler is ~WebSocket -> 'a));
{
    toResponseHandler,
    dataHandler,
    endHandler,
    bodyHandler,
    exceptionHandler,


    putHeaders,
    headers resp =
        (ret = [:];
        forJavaMap (resp is ~HttpClientResponse)#headers() do k v:
            ret.[string k] := if defined? v then string v else "" fi;
        done;
        ret),

    trailers resp =
        (ret = [:];
        forJavaMap (resp is ~HttpClientResponse)#trailers() do k v:
            ret.[string k] := if defined? v then string v else "" fi;
        done;
        ret),

    
    create,

    request,
    generalRequest,
    getNow,
    getBodyNow,
    connectWebsocket,
    
    
}
    
