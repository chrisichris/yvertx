/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

module yeb.validation;

import java.lang:String,IllegalArgumentException;
import java.util.regex:Pattern;
import java.text:NumberFormat, DecimalFormat, DateFormat, SimpleDateFormat;
import java.util: Locale, Date, List,ArrayList, Collections;
import java.net: URLEncoder, URLDecoder;


load yeb.std; 

failWithValidation msg value =
    throw new ValidationException(msg is string, value unsafely_as ~Object);

newValidationException msg value =
    new ValidationException(msg is string, value unsafely_as ~Object);


failVali x = 
    (import java.lang.Throwable;
    import yeti.lang.Tag;
    ob = x unsafely_as ~Object;
    ob =if not defined? x then
            ("" unsafely_as ~Object)
        elif ob instanceof Tag then
            t = (ob unsafely_as ~Tag);
            if t#name == "None" or t#name == "none" then
                t#value
            else
                ob
            fi
        else
            ob
        fi;

    if ob instanceof Throwable then
        throw (ob unsafely_as ~Throwable);
    else 
        failWithValidation "Failure Value: [\(ob)]" ob;
    fi);

validOr notValid validFn value =
    try
        validFn value;
    catch ValidationException ex:
        notValid ();
    yrt;


maybeValid notValidFn valueFn validFn value =
    try
        v = validFn value;
        valueFn v;
    catch ValidationException ex:
        notValidFn ex;
    yrt;
        


valiCheck fun value =
    if fun value then
        value;
    else
        failWithValidation "wrong vaue" value;
    fi;


valiListLength min max listIn is number -> number -> list<'a> -> list<'a>= 
    (l = length listIn;
    if l < min then
        failWithValidation  
            "list length must be minimal \(min) but is \(l)" 
            listIn;
    elif (max > 0) and (l > max) then
        failWithValidation  
            "list length must be maximal \(max) but is \(l)" listIn; 
    else
        listIn
    fi);

valiNum min max num =
    if num > max then
        failWithValidation  
            "number must be max \(max) but is \(num)" num;
    elif (num is number) < (min is number) then
        failWithValidation
            "number must be min \(min) but is \(num)" num;
    else
        num
    fi;


valiList vali listIn = 
    listIn |> map (maybeValid \none Some vali)
        |> catSome;

valiString min max str = 
        if (strLength str) > max then
            failWithValidation
                ("string [\(str)] is \(strLength str) long but should "
                ^"be max \(max) long")
                str;
        elif (strLength str) < min then
            failWithValidation
                ("string [\(str)] is only \(strLength str) long but should "
                ^"be at least \(min) long")
                str;
        else
            str
        fi;



valiRegex regexPattern str =
    if str =~ regexPattern then
      str
    else
        failWithValidation
            "string \(str) does not match pattern \(regexPattern)" 
            str;
    fi;

strToNum str = 
    try 
        number str;
    catch Exception ex:
        failWithValidation "string \(str) is no number" str;
    yrt;

numberFormat formatOpt =
    case formatOpt of
        Format f: f;
        Pattern s: (
             f=NumberFormat#getInstance();
             if f instanceof DecimalFormat then
                df = (f unsafely_as ~DecimalFormat);
                df#applyPattern(s);
             fi;
             f);
        PatternLoc {pattern,loc}: (
             f=NumberFormat#getInstance(loc);
             if f instanceof DecimalFormat then
                df = (f unsafely_as ~DecimalFormat);
                df#applyPattern(pattern);
             fi;
             f);
        None (): NumberFormat#getInstance();
    esac;

strFormatedToNum formatOpt str is 'a -> string -> number=
    try
        format = numberFormat formatOpt;
        format#parse(str)#doubleValue()
    catch Exception e:
        failWithValidation "\(str) is no number" str;
    yrt;

numToFormatedStr formatOpt num =
    (numberFormat formatOpt)#format(num);

strNumberToDate str =
      try
          new Date(number str)
      catch java.lang.Exception ex:
          failWithValidation "\(str) is no number" str;
      yrt;

dateToNumberStr date is ~Date -> string = string date#getTime();

dateFormat formatOpt = 
     case formatOpt of
        Format f: f;
        Pattern s: (new SimpleDateFormat(s)) as ~DateFormat; 
        PatternLoc {pattern,loc}:
           (new SimpleDateFormat(pattern as ~java.lang.String,
                                (loc as ~Locale))) as ~DateFormat;
        None _ : DateFormat#getDateInstance();
     esac;
     
strToBoolean str is string -> boolean=
    if (strLower str) == "true" then true else false fi;

{
    failWithValidation,
    newValidationException,
    validOr, 
    maybeValid,

    

    valiListLength, 
    valiList,

    valiString,
    valiRegex, 
    valiNum,

    valiCheck, 

    strToNum,

    numberFormat, 
    strFormatedToNum,
    numToFormatedStr,
    strNumberToDate, 
    dateToNumberStr, 
    dateFormat, 
    strToBoolean

}
