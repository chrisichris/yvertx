module yvertx.test;

import org.vertx.java.deploy:Container;
import org.vertx.java.core.file:FileProps;

load yeb.std;
load yeb.promise;

load yeb.yvertx;

load yeb.yvertx.testutils;

_ = load yvertx.deploy.child;


fileDir = "../../target/yeti-test-output";

fshelper = {
    clean dir = 
        fileSystem.exists dir 
        >>= do exi:
            if exi then
                fileSystem.delete dir true 
                >>= \(fileSystem.mkdir dir none true)
            else
                fileSystem.mkdir dir none true 
            fi
        done;
};

httpMethod opts method variant rstu =
    (rstu.setTimeout 15000;
    var ssl = false;
    var chunked = false;
    for opts \case of
        SSL b: ssl := b;
        Chunked b: chunked := b;
        esac;

    path = "/someurl/blah.html";
    query = "param1=vparam1&param2=vparam2";
    
    uri = if ssl then "https" else "http" fi
              ^ "://localhost:8080" ^ path ^"?"^query;
    headers = ["header1":"vheader1",
               "header2":"vheader2"];
    rheaders = ["rheader1":"vrheader1",
               "rheader2":"vrheader"];
    
    server = createHttpServerWithHandler do req:
        rstu.assert "uri \(req#uri)" (uri == req#uri);
        rstu.assert "method" (method == req#method);
        rstu.assert "path" (path == req#path);
        rstu.assert "query" (query == req#query);

        rstu.assert "headers" 
            (fold (flip ((and) . (in (requestHeaders req))))
                true (keys headers));
        rstu.assert "params" (params req == ["param1":"vparam1",
                                             "param2":"vparam2"]);
                                            
        putResponseHeaders req rheaders; 
        
        body = newBuffer 0;

        dataHandler req do data:
            _ = body#appendBuffer(data);
        done;

        req#response#setChunked(chunked);

        endHandler req do:
            if not chunked then
                putResponseHeaders req 
                    ["Content-Length":string body#length()];
            fi;
            req#response#writeBuffer(body);
            if chunked then
                putResponseTrailers req ["trailer1" : "vtrailer1",
                                       "trailer2" : "vtrailer2"];
            fi;
            req#response#end();
        done;
    done;

    if ssl then
        server#setSSL(true)
            #setKeyStorePassword("wibble")
            #setKeyStorePath("keystores/server-keystore.jks")
            #setTrustStorePath("keystores/server-truststore.jks")
            #setTrustStorePassword("wibble")
            #setClientAuthRequired(true)
    fi;

    _ = server#listen(8080);

    //wait for server to be ready
    _ = setTimer 1000 do:

        sentBuff = newRandomBuffer(1000);
        
        client = createHttpClient [] "localhost:8080";

        request = httpRequest client (variant uri) [Headers headers]
        do resp:

            rstu.assert "statusCode" (200 == resp#statusCode);
            rstu.assert "rHeaders: \(responseHeaders resp) | \(rheaders)" 
                    (fold (flip ((and) . (in (responseHeaders resp))))
                        true (keys rheaders) );
            body = newBuffer(0);
            dataHandler resp do data:
                _ = body#appendBuffer(data);
            done;

            endHandler resp do:
                rstu.assert "sentBuff" (sentBuff == body);
                if chunked then
                    rstu.assert "rTrailers" (responseTrailers resp ==
                                    ["trailer1":"vtrailer1",
                                    "trailer2":"vtrailer2"]);
                fi;
                closeHttpServer server >>- do:
                    rstu.assure "" true;
                done;
            done;
        done;
        if ssl then
            (client.vertxClient)#setSSL(true)
                #setKeyStorePath("keystores/client-keystore.jks")
                #setKeyStorePassword("wibble")
                #setTrustStorePath("keystores/client-truststore.jks")
                #setTrustStorePassword("wibble");
        fi;

        _ = request#setChunked(chunked);
        if not chunked then
            putRequestHeaders request 
                ["Content-Length": string sentBuff#length()];
        fi;
        _ = request#writeBuffer(sentBuff);
        request#end();
    done);

websocketEcho binary rs =        
    (rs.setTimeout 1000;
  
    server = createHttpServer ();

    connectWebSocketServer server do ws:
        dataHandler ws do buff:
            _ = ws#writeBuffer(buff);
        done;
    done;
    server#listen(8080);

    
    _ = setTimer 1000 do:
        client = createHttpClient [] "localhost:8080";
        
        {buff,str} = if binary then
                {buff = newRandomBuffer 1000, str = ""};
            else
                str = randomString(1000);
                {buff = newStringBuffer str, str};
            fi;

        connectWebSocketClient client "/someurl" do ws:
            received = newBuffer 0;
            dataHandler ws do data:
                _ = received#appendBuffer(data);
                if received#length() == buff#length() then
                    rs.assert "be" (received == buff);
                    (client.vertxClient)#close();
                    closeHttpServer server >>- do:
                        rs.assure "finished" true;
                    done;
                fi;
                if binary then
                    ws#writeBinaryFrame(buff);
                else
                    ws#writeTextFrame(str);
                fi
            done;
        done
    done);

verticle do :
        

    runTests [Exclude "websocket:"] [
"common load": do rs:
   // rs.assure "" ("foo" == load yvertx.common.module1);
   rs.assure "" true;
done,
/*
"deploy" : do rs:
    registerBusHandler "test-handler"
        \(_ = deployVerticle 
                "yvertx.deploy.child.yeti" emptyJS 1 
                \())
        do {body,handler}:
        if body.started then
            rs.assure "in body" true;
            unregisterBusHandler "test-handler" handler \();
        else
        fi
        done;
done,

"undeploy" : do rs:
    _ = deployVerticle "yvertx.deploy.child.yeti" emptyJS 1
        do did:
            registerBusHandler "test-handler" \() do {body,handler}:
                if body.stopped then
                    rs.assure "body-stopped" true;
                    unregisterBusHandler "test-handler" handler \();
                fi
            done;
            undeployVerticle did \();
        done;
done,
*/

"promise:parallel" : do rs:
    //make services 
    adresses = [1..10] |> map (("adr"^) . string);
    for adresses do adr:
        _ = registerBusHandler (Global adr) do {reply, handlerId}:
            reply {value = 1, 
                   for_json=E()};
            _ = unregisterBusHandler handlerId;
        done
    done;
    handlers = adresses |> map do adr:
        busRequest adr emptyJS 
        |>bind do {stru = body} : 
            return body.value 
        done;
    done;
    parallel handlers >>- maybe () do res:
        rs.assure "\(res)" (10 == fold (+) 0 res);
    done
done,

"promise:colllectPromise" : do rs:
    println "\n\n*********************************************\n";
    register adr =
        (_ = registerBusHandler (Global adr) do {reply, handlerId} :
            println "***** msg \(adr)";
            reply {value = adr ^ "foo", for_json=E()};
            _ = unregisterBusHandler handlerId;
        done);
    
    for [1..2] (register . ("adr"^) . string);

    cfn = collectPromises do s:{
         a = setProm s
            (busRequest "adr1" emptyJS |> bind do {stru = js}:
                println "**** value \(js.value)";
                return js.value
            done),
         b = setProm s
            (busRequest "adr2" emptyJS |> bind do {stru = js}:
                println "**** value2 \(js.value)";
                return js.value
            done),
        }done;
        
    cfn >>- maybe () do r :
            println "******** got finayl";
            rs.assure "\(r)" (r.a() == "adr1foo" 
                              and r.b() == "adr2foo");
        done;
done,


"eventbus:simple" : do rs:
    msg = {address = "foo-address", price=23.45, for_json=E()};
    rs.setTimeout(2000);
    address = "foo-some";
    var handled = false;
    _ = registerBusHandler (Global address) do {body, handlerId}:
        _ = unregisterBusHandler handlerId;
        rs.assure "\(body)" (body.stru == msg and not handled);
        handled := true;
    done;
    sendToBus address msg;
done,

"eventbus:emptyMessage" : do rs:
    var handled = false;
    adr = "foo";
    msg = emptyJS;
    _ = registerBusHandler (Local adr) do {body,handlerId}:
        _ = unregisterBusHandler handlerId;
        rs.assure "\(body)" (body.stru == msg and not handled);
        //should do nothing
        _ = unregisterBusHandler handlerId;
        handled := true;
    done;
    sendToBus adr msg;
done,

"eventbus:unregister" : do rs:
    var handled = false;
    msg = {count = 1, foo = "foo", for_json=E()};
    var assertion = true;
    _ = registerBusHandler (Local "foo") do {body ,handlerId}:
        assertion := assertion and (body.stru == msg and not handled);
        _ = unregisterBusHandler handlerId;
        handled := true;
        _ = setTimer 100 do:
            //complete the test
            rs.assure "" assertion;
        done;
    done;
    for [1..3] \(sendToBus "foo" msg);
done,

"eventbus:withReply" : do rs:
    var handled = false;
    msg = {count=1, foo = "foo2", for_json=E()};
    repl = {reply = true, for_json=E()};
    var asserts = true;
    _ = registerBusHandler (Global "foo") do {body,handlerId, reply}:
        asserts := asserts and (not handled and msg == body.stru);
        _ = unregisterBusHandler handlerId;
        handled := true;
        reply repl;
    done;

    busRequest "foo" msg >>- maybe () do {stru}:
        rs.assure "\(stru)" (asserts and (stru == repl));
    done;
done,

"eventbus:replyOfReplyOfReply" : do rs:
    _ = rs.assert is string -> boolean -> ();
    adr = "foo";
    msg = {msg = "message", for_json=E()};
    repl = {msg = "repl", for_json=E()};
    repl2 = {msg = "repl2", for_json=E()};
    repl3 = {msg = "repl3", for_json=E()};
    _ = registerBusHandler (Local adr) do {body, handlerId, request}:
        rs.assert "" (msg == body.stru);
        request repl (maybe () do {body, reply}:
            rs.assert "" (repl2 == body.stru);
            reply repl3;
            _ = unregisterBusHandler handlerId;
        done)
    done;

    prom is promise<'a> = sendToBusWithHandler adr msg; 
    prom >>- maybe () do {body,request}:
        rs.assert "" (body.stru == repl);
        request repl2 (maybe ()do {body}:
            rs.assure "" (body.stru == repl3);
        done)
    done;
done,

"eventbus:emptyReply" : do rs:
    _ = rs.assert is string -> boolean -> ();
    var handled = false;
    adr = "foo";
    msg = emptyJS;
    _ = registerBusHandler (Global adr) do {reply, body, handlerId}:
        rs.assert "" (not handled);
        rs.assert "" (msg == body.stru);
        _ = unregisterBusHandler handlerId;
        handled := true;
        reply emptyJS;
    done;
    busRequest adr msg >>- maybe ()do body:
        rs.assure "" true;
    done;
done,

"filesystem:copy" : do rs:
    dir = fileDir^"/copy";
    from = dir ^"/foo.tmp";
    to = dir ^"/bar.tmp";
    content = "some-data";
    
    pr = fshelper.clean dir 
    >>= \(fileSystem.writeFile from (newStringBuffer content))
    >>= \(fileSystem.copy from to true) 
    >>= \(fileSystem.readFile to);
    pr >>- maybe () do res:
            rs.assure "" ((string res) == content)
        done;
done,

"filesystem:move" : do rs:
    from = fileDir ^"/foo.tmp";
    to = fileDir ^"/bar.tmp";
    content = "some-data";
    fshelper.clean fileDir 
    >>= \(fileSystem.writeFile from (newStringBuffer content)) 
    >>= \(fileSystem.move from to) 
    >>= \(fileSystem.readFile to) 
    >>- maybe () do res:
                rs.assure "" ((string res) == content)
        done
done,    

"filesystem:readDir" : do rs:
    dir = fileDir ^ "/readdir";
    file1 = dir ^"/foo.tmp";
    file2 = dir ^"/bar.tmp";
    file3 = dir ^"/baz.tmp";
    content = newStringBuffer "some-data";
    fshelper.clean dir 
    >>= \(fileSystem.writeFile file1 content)
    >>= \(fileSystem.writeFile file2 content)
    >>= \(fileSystem.writeFile file3 content)
    >>= \(fileSystem.readDir dir none)
    >>- maybe () do res:
        rs.assert "\(res)" (length res == 3);
    done
done,

"filesystem:props" : do rs:
    dir = fileDir ^ "/props";
    content = newStringBuffer "some-data";
    file = dir ^"/foo.tmp";
    fshelper.clean dir 
    >>= \(fileSystem.writeFile file content)
    >>= \(fileSystem.props file)
    >>- maybe () do res:
        rs.assert "" (res is ~FileProps)#isRegularFile;
        rs.assert "" (res#creationTime#getTime() > 0);
        rs.assert "" (res#lastAccessTime#getTime() > 0);
        rs.assure "" (res#lastModifiedTime#getTime() > 0);
    done
done,

"filesystem:pumpFile" : do tests:
    import org.vertx.java.core.file:AsyncFile;
    dir = fileDir^"/pumpfile";
    from = dir^"/foo.tmp";
    to = dir^"/bar.tmp";
    content = newRandomBuffer(10000);
    {
        f () = f1 <- (fshelper.clean dir         
                >>= \(fileSystem.writeFile from content)
                >>= \(fileSystem.open [] from)),
        f1 file1 = f2 file1 <- fileSystem.open [] to,
        f2 file1 file2 =
            rs = (file1 is ~AsyncFile)#getReadStream();
            ws = (file2 is ~AsyncFile)#getWriteStream();
            pump = createPump rs ws;
            pump#start();
            rs#endHandler(toHandler do:
                file1#close(toAsyncResultHandler (maybe ()do:
                    file2#close(toAsyncResultHandler (maybe ()do:
                        fileSystem.readFile to >>- (maybe ()do res:
                            tests.assert "" (content == res);
                        done);
                    done));
                done));
            done);
            return ()
    }.f () >>- \();
done,

"http:hostport" : do rs:
    {host,port} = hostAndPort "localhost:8080";
    rs.assure "" (host == "localhost" and port == 8080);
done,

"http:clientserver" : do rs:
    import java.lang:Thread;
    rs.setTimeout(15000);
    server = (createHttpServerWithHandler do req:
        (putResponseHeaders req 
            ["Content-Type": "text/html; charset=UTF-8"]);
        req#response#end(
            "<html><body><h1>Hello from vert.x!</h1></body></html>");
    done);
    server#listen(8080);

    _ = setTimer 1000 do:
        client = createHttpClient [] "localhost:8080";
        httpRequestNow
            client
            (Get "/") 
            [] 
            >>- maybe ()do {response, body}:
                closeHttpServer server >>- do:
                    rs.assure (string body) true;
                done;
            done;
    done;
done,
    

"http:get" : httpMethod [] "GET" Get,

"http:put" : httpMethod [] "PUT" Put,

"http:post" : httpMethod [] "POST" Post,

//"http:head" : httpMethod [] "HEAD" Head,

"http:options" : httpMethod [] "OPTIONS" Options,

"http:trace" : httpMethod [] "TRACE" Trace,

"http:path" : httpMethod [] "PATCH" Patch,

"net:test1" : do rs:
    server = createAndConnectNetServer do sock:
        dataHandler sock do buffer:
            _ = sock#write(buffer);
        done;
    done;

    server#listen(1234,"localhost");

    serverCloseFn _ = closeNetServer server >>- \(rs.assure "closed" true);

    client = createNetClient ();

    connectNetClient client "localhost:1234" serverCloseFn  
        do sock:
            dataHandler sock do buffer:
                _ = closeNetClient client;
                serverCloseFn();
            done;
            _ = sock#write(newStringBuffer "this is a buffer");
        done;
done,

"parsetools:delimited" : do rs:
    var lineCount = 0;
    
    parser = delimitedRecordParser "\n" do buffer:
        lineCount := lineCount + 1;
        if lineCount == 3 then
            rs.assure "" true;
        fi
    done;

    parser (newStringBuffer "asfdasfsafafafa\nauio\nasdfa\n");
done,

"parsetools:fixed" : do rs:
    var chunkCount = 0;
    chunkSize = 100;
    numChunks = 3;

    parser = fixedRecordParser chunkSize do buffer:
        rs.assert "" (chunkSize == buffer#length());
        chunkCount = chunkCount + 1;
        if chunkCount == numChunks then
            rs.assure "" true;
        fi;
    done;

    input = newBuffer 0;
    for [1..numChunks] do:
        _ = input#appendBuffer(newRandomBuffer chunkSize);
    done;
    parser input;
done,

"timer:oneOff" : do rs:
    rs.setTimeout 5000;

    var count = 0;
    _ = setTimer 1000 do tid:
        rs.assert "count" (count == 0);
        count := count + 1;
        _ = setTimer 10 do:
            rs.assure "" true
        done;
    done;
done,

"timer:periodic" : do rs:
    rs.setTimeout 5000;
    numFires = 20;
    delay = 100;
    var count = 0;
    _ = setPeriodic delay do timerId:
        count := count + 1;
        if count == numFires then
            _ = cancelTimer timerId;
            _ = setTimer 10 do:
                rs.assure "" true;
            done;
        fi;
        if count > numFires then
            rs.assert "Fired too many times" false;
        fi
    done;
done,

"websocket:echoBinary" : websocketEcho true,

"websocket:echoString" : websocketEcho false,

"websocket:writeFromConnectionHandler" : do rs:
    rs.setTimeout 5000;
    server = createHttpServer ();
    connectWebSocketServer server do ws:
        _ = ws#writeTextFrame("foo");
    done;
    server#listen(8080);

    _ = setTimer 1000 do:
        client = createHttpClient [] "localhost:8080";
        connectWebSocketClient client "/someurl" do ws:
            dataHandler ws do buff:
                rs.assert "" ("foo" == string buff);
                (client.vertxClient)#close();
                closeHttpServer server >>- do:
                    rs.assure "close" true;
                done;
            done
        done;
    done;
done,

"websocket:close" : do rs:
    rs.setTimeout 5000;
    server = createHttpServer ();
    connectWebSocketServer server do ws:
        dataHandler ws do:
            ws#close();
        done;
    done;
    server#listen(8080);

    _ = setTimer 1000 do:
        client = createHttpClient [] "localhost:8080";
        connectWebSocketClient client "/someurl" do ws:
            closedWebSocket ws do:
                (client.vertxClient)#close();
                closeHttpServer server >>- do:
                    rs.assure "close" true;
                done;
                ws#writeTextFrame("foo");
            done;
        done;
    done
done,

"websocket:closeFromConnectionHandler" : do rs:
    rs.setTimeout 5000;
    server = createHttpServer ();
    connectWebSocketServer server do ws :
        ws#close();
    done;
    server#listen(8080);

    _ = setTimer 1000 do:
        client = createHttpClient [] "localhost:8080";
        connectWebSocketClient client "/someurl" do ws:
            closedWebSocket ws do:
                (client.vertxClient)#close();
                closeHttpServer server >>- do:
                    rs.assure "close" true;
                done;
            done;
        done;
    done
done,

];
    \()
done;

